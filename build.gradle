plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.10.6'
}

group = 'santannaf.payments'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

repositories {
    mavenCentral()
}

dependencies {
//    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.wildfly.common:wildfly-common:2.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'redis.clients:jedis:6.0.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.bootJar {
    archiveFileName.set("app.jar")
}

tasks.register('runCustomJar', Exec) {
    group = 'application'
    description = 'Run custom jar'
    dependsOn 'bootJar'

    def appName = 'app.jar'
    def addressesBuild = "./build/libs/${appName}"

    commandLine "${System.getenv('JAVA_HOME')}/bin/java",
            '-agentlib:native-image-agent=config-merge-dir=./src/main/resources/META-INF/native-image/,experimental-class-loader-support',
            '-jar',
            addressesBuild
}

graalvmNative {
    binaries {
        main {
            imageName = "payment"
            mainClass = "santannaf.payments.payments.Application"
            configurationFileDirectories.from(file('src/main/resources/META-INF/native-image'))
            verbose = true
            debug = true
            buildArgs.addAll(
                    "-march=compatibility",
                    '--color=always',
                    '--enable-preview',
                    "--allow-incomplete-classpath",
                    "-J-Dfile.enconding=UTF-8"
            )
        }
    }
}
